/*
 * Copyright (c) 2021 2bllw8
 * SPDX-License-Identifier: GPL-3.0-only
 */
buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath libs.android.gradle.plugin
        classpath libs.kotlin.gradle.plugin
    }
}

plugins {
    // Existing plugins
    alias(libs.plugins.compose.compiler) apply false
    alias(libs.plugins.android.application)  apply false
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

final Properties localProps = new Properties()
if (file('local.properties').exists()) {
    localProps.load(file('local.properties').newDataInputStream())
}

ext {
    minSdkVersion = 26
    targetSdkVersion = 34

    sourceCompatibilityVersion = JavaVersion.VERSION_17
    targetCompatibilityVersion = JavaVersion.VERSION_17

    keyStoreFile = localProps.getProperty('androidStoreFile')
    if (keyStoreFile != null && keyStoreFile != '') {
        keyStoreFile = file(keyStoreFile)
    }
    keyStorePassword = localProps.getProperty('androidStorePassword')
    keyAlias = localProps.getProperty('androidKeyAlias')
    keyPassword = localProps.getProperty('androidKeyPassword')
}
